!                       ***********************
                        SUBROUTINE FLUX_HLLC
!                       ***********************

     &(XI,H1,H2,U1,U2,V1,V2,PSI1,PSI2,
     & XNN,YNN,ROT,HLLCFLX)
!
!***********************************************************************
! TELEMAC 2D VERSION 6.2                                         R. ATA
!
!***********************************************************************
!brief 
! 
!     FUNCTION  : SUBROUTINE COMPUTES HLLC FLUX: THREE HYDRODYNAMICAL
!                 COMPENENTS + TRACER TRANSPORT
!      SEE TORO: SHOCK CAPTURING METHODS FOR FREE 
!            SURFACE FLOWS (WILEY 2005)
!
!history  RIADH ATA (EDF R&D-LNHE)
!+        07/15/2012
!+        V6P2
!+
!
!~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
! |  FLX           | <--|  FLUX COMPONENTS AT THE INTERFACE            |
! |  H1,H2         | -->|  LEFT AND RIGHT WATER DEPTHS                 |
! |  PSI1,PSI2     | -->|  LEFT AND RIGHT TRACER DENSITIES             |
! |  ROT           | -->|  EXECUTE FINAL ROTATION OR NO                |
! |  U1,U2         | -->|  LEFT AND RIGHT VELOCITY X-COMPONENTS        |
! |  V1,V2         | -->|  LEFT AND RIGHT VELOCITY Y-COMPONENTS        |
! |  XNN,YNN       | -->|  X AND Y COMPONENT OF THE OUTWARD NORMAL     |
! ______________________________________________________________________
!
!  MODE: -->(UNCHANGEABLE INPUT),<--(OUTPUT),<-->(CHANGEABLE INPUT)   
!-----------------------------------------------------------------------
!  CALLING SUBROUTINE FLUX_WAF OR FLUX_HLLC OR FLUXZZ 
! 
!***********************************************************************
!
      USE BIEF
!
      IMPLICIT NONE
      INTEGER LNG,LU
      COMMON/INFO/LNG,LU
!     
!+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
!
      DOUBLE PRECISION, INTENT(IN)    :: H1,H2,U1,U2,PSI1,PSI2
      DOUBLE PRECISION, INTENT(IN)    :: XI,V1,V2,XNN,YNN
      LOGICAL, INTENT(IN)             :: ROT
      DOUBLE PRECISION, INTENT(INOUT) :: HLLCFLX(4)
!***********************************************************************
!
      INTEGER                         :: I,SPY
      DOUBLE PRECISION, PARAMETER     :: G = 9.81D0
      DOUBLE PRECISION                :: HL,UL,VL,PSI_L
      DOUBLE PRECISION                :: HR,UR,VR,PSI_R
      DOUBLE PRECISION                :: AL,AR,HSTAR,USTAR
      DOUBLE PRECISION                :: pQL,pQR,SL,SR
      DOUBLE PRECISION                :: QSTARL(4),QSTARR(4)
      DOUBLE PRECISION                :: QL(4),QR(4),FL(4),FR(4)
      DOUBLE PRECISION                :: FSTARL(4),FSTARR(4)
!
      DOUBLE PRECISION                :: GSUR2,EPS
      DOUBLE PRECISION                :: FLU2X,FLU2Y
      DOUBLE PRECISION                :: U0,POND,SSTAR
      DOUBLE PRECISION                :: FLX(4)
!-----------------------------------------------------------------------
       EPS   = 1.E-6
       GSUR2 = G/2.0D0
       SPY   = 0
!***********************************************************************
! INITIALIZATION OF FLX AND HLLCFLX
      DO I=1,4
         FLX(I)     = 0.0D0
         HLLCFLX(I) = 0.0D0
      ENDDO
!
!-----------------------------------------------------------------------
! DEPTHS, VELOCITIES, TRACERS
      HL    = H1
      UL    = U1
      VL    = V1
      PSI_L = PSI1

      HR    = H2
      UR    = U2
      VR    = V2
      PSI_R = PSI2
!
! ROTATION
!
      U0 = UL
      UL  = XNN*U0+YNN*VL
      VL  =-YNN*U0+XNN*VL
!
      U0 = UR
      UR  = XNN*U0+YNN*VR
      VR  =-YNN*U0+XNN*VR
!
10    CONTINUE
! CELERITIES
!
      AL = SQRT(G*HL)
      AR = SQRT(G*HR)
! STAR VARIABLES
      HSTAR = 0.5D0*(HL+HR)-0.25D0*(UR-UL)*(HL+HR)/(AL+AR)
      USTAR = 0.5D0*(UL+UR)-0.25D0*(HR-HL)*(AL+AR)/(HL+HR)
! COMPUTE pQL AND pQR:
! IT WILL DEPEND IF WE ARE IN PRESENCE OF SHOCK OR RAREFACTION WAVE 
      IF(HSTAR.LT.HL)THEN
!        RAREFACTION
         pQL = 1.0D0
      ELSE
!        SHOCK 
         IF(HL.GT.EPS)THEN
            pQL = SQRT(0.5D0*(HSTAR+HL)*HSTAR/HL**2)
         ELSE
            pQL = 0.0D0
         ENDIF
      ENDIF
      IF(HSTAR.LT.HR)THEN
!        RAREFACTION
         pQR = 1.0D0
      ELSE
!        SHOCK
         IF(HR.GT.EPS)THEN
            pQR = SQRT(0.5D0*(HSTAR+HR)*HSTAR/HR**2)
         ELSE
            pQR = 0.0D0
         ENDIF
      ENDIF

! COMPUTE SL, SR AND SSTAR  (WE CONSIDER DRY CASES)
      IF(HL.GT.EPS)THEN
         SL = UL-AL*pQL
      ELSE
         SL = UR - 2.0D0*AR
         SR = UR + AR
      ENDIF

      IF(HR.GT.EPS)THEN
         SR = UR + AR*pQR
      ELSE
         SL = UL - AL
         SR = UL + 2.0D0*AL
      ENDIF
      SSTAR = USTAR

! COMPUTE QL AND QR
      QL(1)     = HL
      QL(2)     = HL*UL
      QL(3)     = HL*VL
      QL(4)     = HL*PSI_L

      QR(1)     = HR
      QR(2)     = HR*UR
      QR(3)     = HR*VR
      QR(4)     = HR*PSI_R

! COMPUTE QSTARL AND QSTARR
      POND      = HL*( (SL-UL)/(SL-SSTAR) )
      QSTARL(1) = POND
      QSTARL(2) = POND*SSTAR
      QSTARL(3) = POND*VL
      QSTARL(4) = POND*PSI_L

      POND      = HR*( (SR-UR)/(SR-SSTAR) ) 
      QSTARR(1) = POND
      QSTARR(2) = POND*SSTAR
      QSTARR(3) = POND*VR
      QSTARR(4) = POND*PSI_R

! COMPUTE FL AND FR
      FL(1)     = HL*UL
      FL(2)     = HL*UL**2 +GSUR2*HL**2
      FL(3)     = HL*UL*VL
      FL(4)     = HL*UL*PSI_L 

      FR(1)     = HR*UR
      FR(2)     = HR*UR**2 +GSUR2*HR**2
      FR(3)     = HR*UR*VR
      FR(4)     = HR*UR*PSI_R 

! COMPUTE FSTARL SFTARR 
      FSTARL(1) = FL(1) + SL*(QSTARL(1)-QL(1))
      FSTARL(2) = FL(2) + SL*(QSTARL(2)-QL(2))
      FSTARL(3) = FL(3) + SL*(QSTARL(3)-QL(3))
      FSTARL(4) = FL(4) + SL*(QSTARL(4)-QL(4))

      FSTARR(1) = FR(1) + SR*(QSTARR(1)-QR(1))
      FSTARR(2) = FR(2) + SR*(QSTARR(2)-QR(2))
      FSTARR(3) = FR(3) + SR*(QSTARR(3)-QR(3))
      FSTARR(4) = FR(4) + SR*(QSTARR(4)-QR(4))
! AND FINALLY THE HLLC FLUX (BEFORE ROTATION)
      IF(XI.LT.SL)THEN
        FLX(1) = FL(1)
        FLX(2) = FL(2)
        FLX(3) = FL(3)
        FLX(4) = FL(4)
        SPY = 1
      ELSEIF(XI.LT.SSTAR.AND.XI.GT.SL) THEN
        FLX(1) = FSTARL(1)
        FLX(2) = FSTARL(2)
        FLX(3) = FSTARL(3)
        FLX(4) = FSTARL(4)
        SPY = 1
      ELSEIF(XI.GT.SSTAR.AND.XI.LT.SR) THEN
        FLX(1) = FSTARR(1)
        FLX(2) = FSTARR(2)
        FLX(3) = FSTARR(3)
        FLX(4) = FSTARR(4)
        SPY = 1
      ELSE
        FLX(1) = FR(1)
        FLX(2) = FR(2)
        FLX(3) = FR(3)
        FLX(4) = FR(4)
        SPY = 1
      ENDIF
      IF(SPY.EQ.0)THEN
        WRITE(LU,*)'ERROR IN HLLC FLUX ESTIMATION (FLUX_HLLC.F)'
        CALL PLANTE(1)
        STOP
      ENDIF
!
! INVERSE ROTATION AND FINAL FLUX
!
       IF(ROT)THEN
         FLU2X  = XNN*FLX(2) - YNN*FLX(3) 
         FLU2Y  = YNN*FLX(2) + XNN*FLX(3)
!
         HLLCFLX(1) = FLX(1)
         HLLCFLX(2) = FLU2X 
         HLLCFLX(3) = FLU2Y
         HLLCFLX(4) = FLX(4) 
       ELSE
! IN THIS CASE, NO ROTATION
!
! FINAL FLUX 
!
         HLLCFLX(1) = FLX(1)
         HLLCFLX(2) = FLX(2) 
         HLLCFLX(3) = FLX(3)
         HLLCFLX(4) = FLX(4) 
       ENDIF
!
50    CONTINUE
!
!-----------------------------------------------------------------------
!
      RETURN
      END
